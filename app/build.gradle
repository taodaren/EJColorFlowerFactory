apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.box_tech.fireworksmachine"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode getGitVersionCode()
        versionName "${getGitVersion()}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    android.applicationVariants.all {
        variant ->
            variant.outputs.all {
                def built_type = variant.getBuildType().getName()
                //def releaseTime = new Date().format("yyyyMMddhh", TimeZone.getTimeZone("GMT+8"))
                outputFileName = "彩花机_${variant.versionName}_${built_type}.apk"
            }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

static def getGitVersion(){
    return 'git describe --tags --abbrev=4'.execute().text.trim()
}

static def getGitVersionCode(){
    return Integer.parseInt('git rev-list HEAD --count'.execute().text.trim())
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.google.code.gson:gson:2.8.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.7.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.yanzhenjie:permission:2.0.0-rc2'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.lantouzi.wheelview:library:1.1.2'
    implementation 'cn.carbswang.android:NumberPickerView:1.1.1'
    //testCompile 'junit:junit:4.12'
    implementation files('libs/commons-codec-1.11.jar')
}
